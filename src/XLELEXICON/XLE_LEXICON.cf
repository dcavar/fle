{-
    XLE Lexicon Converter: Language definition
    Copyright (C) 2016  Author: Damir Cavar, Lwin Moe

   Copyright 2016 Damir Cavar

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.


   Sample grammar to parse:

TOY   ENGLISH   LEXICON (1.0)

is 	  AUX * (^ TENSE)=PRES
		(^ ASPECT)=PROG.

in 	  P * @(PREP IN).

with 	  P * @(PREP WITH).

the 	  D * @(DET DEF).

a 	  D * @(DET INDEF)
	      @(NUMBER SG).

girl 	  N * @(N-SG GIRL).

boys 	  N * @(N-PL BOY).

banana 	  N * @(N-SG BANANA).

park 	  N * @(N-SG PARK).

telescope N * @(N-SG TELESCOPE).

sheep 	  N * { @(N-SG SHEEP)
	       |@(N-PL SHEEP)}.

Mary      N * @(N-SG MARY).

devours   V * @(TRANS DEVOUR)
	      @V3SG.

devoured  V * @(TRANS DEVOUR)
	      @VPAST.

saw 	  V * @(TRANS SEE)
	      @VPAST.

sleeping  V * @(INTRANS SLEEP)
	      (^ ASPECT)=c PROG.

sleeps 	  V * @(INTRANS SLEEP)
	      @V3SG.

walks 	  V * @(INTRANS WALK)
	      @V3SG.

-}

comment "\"" "\"" ;

token Word ( letter | digit | '_' | '-' | '+')+ ;

Lexicon.    LEXICON ::= [RULE] ;
RuleS.      RULE    ::= WORD RULEDEF ;
RuleS2.     RULE    ::= RULEDEF ;

RuleDef.        RULEDEF ::= CAT MORPHCODE [SCHEM] ;
RuleDefS.       RULEDEF ::= RULEDEF ";" RULEDEF ;
RuleDefSS.      RULEDEF ::= CAT MORPHCODE Word ;
RuleDefSE.      RULEDEF ::= CAT MORPHCODE ;
RuleDefSEEtc.   RULEDEF ::= "ETC" ;
RuleDefSP.      RULEDEF ::= [MORPHCODE] Word [MORPHCODE];
RuleDefSP2.      RULEDEF ::= [MORPHCODE] Word [MORPHCODE] SCHEM;
terminator RULE "." ;

WordS.      WORD    ::= Word ;
WordSP.     WORD    ::= "+" Word ;
WordSPP.    WORD    ::= Word "+" ;
WordSPAP.   WORD    ::= Word "`" Word ;
WordSDP.    WORD    ::= "+" Word "+" ;

CatS.       CAT  ::= Word ;
CatSE.      CAT  ::= "!" Word ;
CatP.       CAT  ::= "+" Word ;
CatPS.      CAT  ::= Word "+";

MorphcodeS.     MORPHCODE ::= "*" ;
MorphcodeX.     MORPHCODE ::= "XLE" ;
MorphcodePQM.   MORPHCODE ::= "?" ;
MorphcodePC.    MORPHCODE ::= "," ;
MorphcodeSC.    MORPHCODE ::= ";" ;
MorphcodePQD.   MORPHCODE ::= "`";
MorphcodePQC.   MORPHCODE ::= "." ;
MorphcodePSQ.   MORPHCODE ::= "'" ;
MorphcodePSQB.   MORPHCODE ::= "[" ;
MorphcodePSQBR.   MORPHCODE ::= "]" ;

terminator MORPHCODE "";

Schema.                 SCHEM ::= LEFTSCHEMA "=" RIGHTSCHEMA ;
SchemaB.                SCHEM ::= [LEFTSCHEMA] "=" [RIGHTSCHEMA] ;
SchemaC.                SCHEM ::= LEFTSCHEMA "=c" RIGHTSCHEMA ;
SchemaD.                SCHEM ::= LEFTSCHEMA "~=" RIGHTSCHEMA ;
SchemaNegD.             SCHEM ::= "~" LEFTSCHEMA ;
SchemaTemplate.         SCHEM ::= TEMPLATERULE ;
SchemaDolOpt.           SCHEM ::= LEFTSCHEMA "$" "{" [SYMBOL] "}" ;
SchemaDolOptA.          SCHEM ::= LEFTSCHEMA "$" RIGHTSCHEMA ;
SchemaTemplateDisjoint. SCHEM ::= TEMPLATEDISJOINTRULE ;
SchemaDisjoint.         SCHEM ::= "{" [DISJOINTSCHEMA] "}" ;

DisjSchema.             DISJOINTSCHEMA ::= [DSCHEM] ;

DSchem.                 DSCHEM ::= LEFTSCHEMA "=" RIGHTSCHEMA ;
DSchemB.                DSCHEM ::= LEFTSCHEMA "=c" RIGHTSCHEMA ;
DSchemE.                DSCHEM ::= LEFTSCHEMA "~=" RIGHTSCHEMA ;
DSchemC.                DSCHEM ::= "(" UP SYMBOL ")" ;
DSchemD.                DSCHEM ::= "(" DOWN SYMBOL ")" ;

--DSchemTemplate. DSCHEM ::= TEMPLATERULE ;

separator DISJOINTSCHEMA "|" ;
separator DSCHEM "" ;

LSchemaUP.         LEFTSCHEMA ::= UP ;
LSchemaDOWN.       LEFTSCHEMA ::= DOWN ;
LSchemaUpSymbol.   LEFTSCHEMA ::= "(" UP SYMBOL ")" ;
LSchemaUpSymbolD.  LEFTSCHEMA ::= "(" UP SYMBOL SYMBOL ")" ;
LSchemaUpSymbolT.  LEFTSCHEMA ::= "(" UP SYMBOL SYMBOL SYMBOL ")" ;
LSchemaUpSymbolT2. LEFTSCHEMA ::= "(" UP SYMBOL SYMBOL SYMBOL SYMBOL ")" ;
LSchemaDownSymbol. LEFTSCHEMA ::= "(" DOWN SYMBOL ")" ;
LSchemaPWord.      LEFTSCHEMA ::= "%" Word ;
LSchemaPWordB.     LEFTSCHEMA ::= "(" "%" SYMBOL SYMBOL ")" ;
LSchemaPWordBD.    LEFTSCHEMA ::= "(" "%" SYMBOL SYMBOL SYMBOL ")" ;
LSchemaPWordBD2.   LEFTSCHEMA ::= "(" "%" SYMBOL SYMBOL SYMBOL SYMBOL ")" ;

RSchemaSymbol.  RIGHTSCHEMA ::= SYMBOL;
RSchemaSymbolE. RIGHTSCHEMA ::= "'" SYMBOL "'" ;
RSchemaDown.    RIGHTSCHEMA ::= DOWN;
RSchemaUpS.     RIGHTSCHEMA ::= "(" UP SYMBOL ")" ;
RschemaP.       RIGHTSCHEMA ::= "+" ;
RschemaM.       RIGHTSCHEMA ::= "-" ;
RschemaStem.    RIGHTSCHEMA ::= "%stem" ;

TemlateDisjointRule.    TEMPLATEDISJOINTRULE ::= "{" [TEMPLATERULEORHS] "}" ;
TemlateDisjointRuleA.   TEMPLATEDISJOINTRULE ::= "{" "{" [TEMPLATERULEORHS] "}" [TEMPLATERULEORHS] "}";

TemplateORRule.         TEMPLATERULEORHS ::= [TEMPLATERULE] ;

TemplateSingleRule.     TEMPLATERULE  ::= "@" SYMBOL;
TemplateRule.           TEMPLATERULE  ::= "@" TEMPLATE ;
TemplateRuleRB.         TEMPLATERULE  ::= "@" "(" SYMBOL ")" ;
TemplateRuleRBP.        TEMPLATERULE  ::= "@" "(" SYMBOL "+" SYMBOL ")" ;
TemplateRuleRBM.        TEMPLATERULE  ::= "@" "(" SYMBOL "-" SYMBOL ")" ;
TemplateRuleRBPerc.     TEMPLATERULE  ::= "@" "(" SYMBOL "%" SYMBOL SYMBOL ")" ;
TemplateRuleRBPercS.    TEMPLATERULE  ::= "@" "(" SYMBOL UP SYMBOL ")" ;
TemplateRuleRBPercSS.   TEMPLATERULE  ::= "@" "(" SYMBOL UP SYMBOL SYMBOL ")" ;
TemplateRuleRBPercSSU.  TEMPLATERULE  ::= "@" "(" SYMBOL "(" UP SYMBOL SYMBOL ")" SYMBOL ")";
TemplateRuleRBPercSSS.  TEMPLATERULE  ::= "@" "(" SYMBOL UP SYMBOL SYMBOL SYMBOL ")" ;
TemplateRuleRBA.        TEMPLATERULE  ::= "@" "(" SYMBOL "@" SYMBOL ")" ;
TemplateRuleRBDisj.     TEMPLATERULE  ::= "@" "(" SYMBOL "{" [TEMPLATERULEORHS] "}" ")" ;


TemplateRuleRBB.        TEMPLATERULE  ::= "~" "(" UP SYMBOL ")" ;

TemplateRuleRBGENS.  TEMPLATERULE  ::= "(" UP SYMBOL SYMBOL SYMBOL ")" "=" SYMBOL ;
TemplateRuleVoice.   TEMPLATERULE  ::= "(" UP SYMBOL SYMBOL SYMBOL ")" "=" PLUS ;
TemplateRuleExcep.   TEMPLATERULE  ::=  "(" UP SYMBOL ")" "=c" "+" "@" "(" SYMBOL SYMBOL PLUS ")" ;
TemplateRuleExcep2.   TEMPLATERULE  ::=  "(" UP SYMBOL ")" "=c" SYMBOL ;

TemplateRuleS.       TEMPLATE      ::= "(" SYMBOL SYMBOL ")";
TemplateRuleSP.      TEMPLATE      ::= "(" SYMBOL "+" ")" ;
TemplateRuleSM.      TEMPLATE      ::= "(" SYMBOL "-" ")" ;
TemplateRuleST.      TEMPLATE      ::= "(" SYMBOL SYMBOL SYMBOL ")";
TemplateRuleSST.     TEMPLATE      ::= "(" SYMBOL SYMBOL SYMBOL SYMBOL ")";
TemplateRuleX.       TEMPLATE      ::= "(" SYMBOL "%stem" ")" ;
TemplateRuleXS.      TEMPLATE      ::= "(" SYMBOL "%stem" SYMBOL ")" ;


Symbol.     SYMBOL ::= Word ;

Up.   UP   ::= "^" ;
Down. DOWN ::= "!" ;
Plus. PLUS ::= "+" ;

separator RULE "" ;
separator WORD "" ;
separator CAT "" ;
separator MORPHCODE "" ;
separator SCHEM "" ;
separator LEFTSCHEMA "";
separator RIGHTSCHEMA "";
separator TEMPLATERULEORHS "|";
separator TEMPLATERULE "";
separator SYMBOL "" ;
separator UP "";
separator DOWN "";
separator PLUS "";