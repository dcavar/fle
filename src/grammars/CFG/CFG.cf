{-
    Context-free Gramamr (CFG) Converter: Language definition
    Copyright (C) 2015,2016  Author: Damir Cavar

   Copyright 2015,2016 Damir Cavar

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.

A very simple grammar formalism:

Well-formed:

S --> NP VP
NP --> {Art|Q} N
NP --> (Art) Adj* N

Not well-formed:
NP --> {Art+|Q*} N
NP --> (Art+) Adj* N
-}

comment "#" ;

Gram. Grammar ::= [Rule] ;
Rul.  Rule    ::= LHS "-->" [RHS] ;
ERul. Rule    ::=  ;

terminator Rule "\\n" ;

LhsS. LHS ::= Ident ;

RhsDisjSyms.     DRHS ::= Ident ;
RhsDisjSymsP.    DRHS ::= Ident "+" ;
RhsDisjSymsA.    DRHS ::= Ident "*" ;
RhsDisjTerminal. DRHS ::= String ;
RhsDisj.         RHS  ::= "{" [DRHS] "}" ;

RhsBrhsS.  BRHS ::= Ident ;
RhsBrhsSP. BRHS ::= Ident "+" ;
RhsBrhsSA. BRHS ::= Ident "*" ;
RhsBrhsTerminal. BRHS ::= String ;

RhsSymbol.   RHS ::= Ident ;
RhsSymbolP.  RHS ::= Ident "+" ;
RhsSymbolA.  RHS ::= Ident "*" ;
RhsTerminal. RHS ::= String ;
RhsEpsilon.  RHS ::= "e" ;

RhsBr.     RHS ::= "(" [BRHS] ")" ;
RhsBrPlus. RHS ::= "(" [BRHS] ")" "+" ;
RhsBrAst.  RHS ::= "(" [BRHS] ")" "*" ;

separator RHS  "" ;
separator BRHS "" ;
separator DRHS "|" ;
